module utility where

open import Algebra
  using (Monoid)
open import Algebra.Structures
  using (IsMonoid ; IsSemigroup)
open import Data.Empty
open import Function
  using (_âˆ˜_ ; _$_ ; _âˆ‹_)
open import Relation.Binary
  using (Decidable)
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_ ; refl ; setoid ; sym ; cong ; subst)
open import Relation.Nullary
open import Relation.Nullary.Decidable
  using (âŒŠ_âŒ‹)
open import Data.Bool
  using (not)
open import Data.List hiding (map)
open import Data.List.Properties
  using (map-id ; map-compose ; map-cong)
open import Data.List.Any as ListAny
  using (Any ; any ; here ; there)
open import Data.List.Any.Properties using ( âˆ·â†” ; ++â»)
  renaming (++âºË¡ to ++Ë¡ ; ++âºÊ³ to ++Ê³)
open import Data.Nat
  using (â„• ; zero ; suc ; _â‰Ÿ_ ; _+_ ; _âˆ¸_)
open import Data.Nat.Properties.Simple
  using (+-comm)
open import Data.Nat.Properties
  using (nâˆ¸nâ‰¡0 ; m+nâˆ¸nâ‰¡m)
open import Data.Product as Prod
  using (_,_ ; _,â€²_ ; _Ã—_ ; projâ‚ ; projâ‚‚ ; âˆƒ)
open import Data.Sum
  using (_âŠ_ ; injâ‚ ; injâ‚‚)
open import Function
  using (_âˆ˜_ ; id)
import Data.List.Membership.Setoid

++-assoc : âˆ€{A : Set} â†’ (xs ys zs : List A) â†’ (xs ++ ys) ++ zs â‰¡ xs ++ (ys ++ zs)
++-assoc {_} xs ys zs =
  IsSemigroup.assoc (IsMonoid.isSemigroup (Monoid.isMonoid (Data.List.Properties.++-monoid _))) xs _ _



_âˆˆ_ : {A : Set} â†’ (x : A) â†’ (xs : List A) â†’ Set
_âˆˆ_ {A} x xs = Data.List.Membership.Setoid._âˆˆ_ (setoid A) x xs

_âˆ‰_ : {A : Set} â†’ (x : A) â†’ (xs : List A) â†’ Set
_âˆ‰_ {A} x xs = Data.List.Membership.Setoid._âˆ‰_ (setoid A) x xs



map-second : {A B C D : Set} â†’
  (f : B â†’ D) â†’
  A Ã— B Ã— C â†’ A Ã— D Ã— C
map-second f = Prod.map id (Prod.map f id)

any-mapâº : âˆ€ {A B} {xs : List A} {x} (f : A â†’ B) â†’
  x âˆˆ xs â†’ f x âˆˆ Data.List.map f xs
any-mapâº f (here refl)  = here refl
any-mapâº f (there xâˆˆxs) = there (any-mapâº f xâˆˆxs)

-- the map-mono like the one in Data.List.Any.Membership
-- idk why I can't import that module so I just implement one here
map-mono : âˆ€ {A B} {ys zs : List A} (f : A â†’ B) â†’
  (âˆ€ x â†’ x âˆˆ ys â†’ x âˆˆ zs) â†’ âˆ€ fx â†’ fx âˆˆ Data.List.map f ys â†’ fx âˆˆ Data.List.map f zs
map-mono {ys = []} f ysâŠ†zs fx ()
map-mono {ys = y âˆ· ys} {zs} f ysâŠ†zs .(f y) (here refl) =
  any-mapâº f (ysâŠ†zs y (here refl))
map-mono {ys = y âˆ· ys} {zs} f ysâŠ†zs fx (there fxâˆˆmap-f-ys) =
  map-mono f (Î» x z â†’ ysâŠ†zs x (there z)) fx fxâˆˆmap-f-ys

any-mapâºÂ² : âˆ€ {A B : Set} {xs x fxy} (f : A â†’ List B) â†’
  x âˆˆ xs â†’ fxy âˆˆ f x â†’ fxy âˆˆ concatMap f xs
any-mapâºÂ² f (here refl)  fxyâˆˆfx = ++Ë¡ fxyâˆˆfx
any-mapâºÂ² f (there xâˆˆxs) fxyâˆˆfx = ++Ê³ (f _) (any-mapâºÂ² f xâˆˆxs fxyâˆˆfx)

map-monoÂ² : âˆ€ {A B C} {xs xs' : List A} {ys ys' : List B} (f : A â†’ B â†’ C) â†’
  (âˆ€ u â†’ u âˆˆ xs â†’ u âˆˆ xs') â†’
  (âˆ€ v â†’ v âˆˆ ys â†’ v âˆˆ ys') â†’
  âˆ€ fuv â†’
    fuv âˆˆ concatMap (Î» u â†’ Data.List.map (f u) ys)  xs â†’
    fuv âˆˆ concatMap (Î» u â†’ Data.List.map (f u) ys') xs'
map-monoÂ² {xs = []} {xs'} {ys} {ys'} f xsâŠ†xs' ysâŠ†ys' fuv ()
map-monoÂ² {xs = x âˆ· xs} {xs'} {ys} {ys'} f xsâŠ†xs' ysâŠ†ys' fuv fuvâˆˆ[[fuv|ys]|xâˆ·xs]
  with ++â» (Data.List.map (f x) ys) fuvâˆˆ[[fuv|ys]|xâˆ·xs]
... | injâ‚‚ fuvâˆˆ[[fuv|ys]|xs] =
  map-monoÂ² f (Î» u â†’ xsâŠ†xs' u âˆ˜ there) ysâŠ†ys' fuv fuvâˆˆ[[fuv|ys]|xs]
... | injâ‚ fuvâˆˆ[fxv|ys] =
  any-mapâºÂ² (Î» u â†’ Data.List.map (f u) ys')
    (xsâŠ†xs' x (here refl))
    (map-mono (f x) ysâŠ†ys' fuv fuvâˆˆ[fxv|ys])



module ListSet {A : Set} (_â‰Ÿ_ : Decidable {A = A} _â‰¡_) where
  open import Relation.Nullary.Negation using (Â¬?)
  ST : Set
  ST = List A


  set-subtract : List A â†’ List A â†’ List A
  set-subtract S T = filter (Î» x â†’ Â¬? (ListAny.any (_â‰Ÿ_ x) T)) S

  set-remove : List A â†’ A â†’ List A
  set-remove S x = filter (Â¬? âˆ˜ _â‰Ÿ_ x) S

  set-remove-removed : âˆ€{S L} â†’ S âˆ‰ set-remove L S
  set-remove-removed {S} {[]} ()
  set-remove-removed {S} {x âˆ· L} Sin with S â‰Ÿ x | set-remove-removed{S}{L}
  set-remove-removed {S} {x âˆ· L} Sin | yes refl | r = r Sin
  set-remove-removed {S} {x âˆ· L} (here px) | no Â¬p | r = Â¬p px
  set-remove-removed {S} {x âˆ· L} (there Sin) | no Â¬p | r = r Sin

  set-remove-not-eq : âˆ€{a a' xs} â†’ a âˆˆ set-remove xs a' â†’ Â¬ a' â‰¡ a
  set-remove-not-eq {a}{a'}{xs} aâˆˆxs-a' with a' â‰Ÿ a
  ... | yes refl = âŠ¥-elim (set-remove-removed{a}{xs} aâˆˆxs-a')
  ... | no Â¬refl = Â¬refl

  set-remove-mono-âˆˆ : âˆ€ {xs a} a' â†’ a âˆˆ set-remove xs a' â†’ a âˆˆ xs
  set-remove-mono-âˆˆ {[]}       {a} a' ()
  set-remove-mono-âˆˆ {(x âˆ· xs)} {a} a' aâˆˆxs-[a'] with a' â‰Ÿ x
  set-remove-mono-âˆˆ {(x âˆ· xs)} {a} a' aâˆˆxs-[a']
    | yes a'â‰¡x = there (set-remove-mono-âˆˆ a' aâˆˆxs-[a'])
  set-remove-mono-âˆˆ {(x âˆ· xs)} {a} a' (here refl)
    | no Â¬a'â‰¡x = here refl
  set-remove-mono-âˆˆ {(x âˆ· xs)} {a} a' (there aâˆˆxs-[a'])
    | no Â¬a'â‰¡x = there (set-remove-mono-âˆˆ a' aâˆˆxs-[a'])

  set-remove-not-removed : âˆ€ {a' a xs} â†’ Â¬ (a' â‰¡ a) â†’ a âˆˆ xs â†’ a âˆˆ set-remove xs a'
  set-remove-not-removed {a'} {a} {.a âˆ· xs} a'â‰¢a (here refl)  with a' â‰Ÿ a
  ... | yes a'â‰¡a = âŠ¥-elim (a'â‰¢a a'â‰¡a)
  ... | no Â¬a'â‰¡a = here refl
  set-remove-not-removed {a'} {a} {x âˆ· xs} a'â‰¢a (there aâˆˆxs) with a' â‰Ÿ x
  ... | yes a'â‰¡x = set-remove-not-removed a'â‰¢a aâˆˆxs
  ... | no Â¬a'â‰¡x = there (set-remove-not-removed a'â‰¢a aâˆˆxs)

  set-subtract-[] : âˆ€ xs â†’ set-subtract xs [] â‰¡ xs
  set-subtract-[] [] = refl
  set-subtract-[] (x âˆ· xs) rewrite set-subtract-[] xs = refl

  set-subtract-[a]â‰¡set-remove : âˆ€ xs a â†’ set-subtract xs (a âˆ· []) â‰¡ set-remove xs a
  set-subtract-[a]â‰¡set-remove [] a = refl
  set-subtract-[a]â‰¡set-remove (x âˆ· xs) a
    with x â‰Ÿ a | a â‰Ÿ x
  ... | yes refl | no  aâ‰¢x  = âŠ¥-elim (aâ‰¢x refl)
  ... | no  xâ‰¢a  | yes refl = âŠ¥-elim (xâ‰¢a refl)
  ... | yes refl | yes aâ‰¡x =  set-subtract-[a]â‰¡set-remove xs x 
  ... | no  xâ‰¢a  | no  aâ‰¢x  rewrite set-subtract-[a]â‰¡set-remove xs a =  refl 

  set-subtract-merge : âˆ€ {xs ys z} â†’ z âˆˆ set-subtract xs ys â†’ (z âˆˆ xs) Ã— (z âˆ‰ ys)
  set-subtract-merge {[]} ()
  set-subtract-merge {x âˆ· xs} {ys} zâˆˆxs-ys with ListAny.any (_â‰Ÿ_ x) ys
  set-subtract-merge {x âˆ· xs} {ys} (here refl)     | no xâˆ‰ys = here refl ,â€² xâˆ‰ys
  set-subtract-merge {x âˆ· xs} {ys} (there zâˆˆxs-ys) | no xâˆ‰ys = there zâˆˆxs , zâˆ‰ys
    where zâˆˆxs,zâˆ‰ys = set-subtract-merge {xs} {ys} zâˆˆxs-ys
          zâˆˆxs      = projâ‚ zâˆˆxs,zâˆ‰ys
          zâˆ‰ys      = projâ‚‚ zâˆˆxs,zâˆ‰ys
  ... | yes xâˆˆys = there zâˆˆxs , zâˆ‰ys
    where zâˆˆxs,zâˆ‰ys = set-subtract-merge {xs} {ys} zâˆˆxs-ys
          zâˆˆxs      = projâ‚ zâˆˆxs,zâˆ‰ys
          zâˆ‰ys      = projâ‚‚ zâˆˆxs,zâˆ‰ys

  set-subtract-split : âˆ€ {xs ys z} â†’ z âˆˆ xs â†’ z âˆˆ set-subtract xs ys âŠ z âˆˆ ys
  set-subtract-split {[]}     ()
  set-subtract-split {x âˆ· xs} {ys} (here refl) with ListAny.any (_â‰Ÿ_ x) ys
  ... | yes xâˆˆys = injâ‚‚ xâˆˆys
  ... | no  xâˆ‰ys = injâ‚ ( (here refl) )
  set-subtract-split {x âˆ· xs} {ys} (there xâˆˆxs)
    with ListAny.any (_â‰Ÿ_ x) ys | set-subtract-split xâˆˆxs
  ... | xâˆˆysâŠxâˆ‰ys | injâ‚‚ zâˆˆys    = injâ‚‚ zâˆˆys
  ... | yes xâˆˆys  | injâ‚ zâˆˆxs-ys = injâ‚  zâˆˆxs-ys 
  ... | no  xâˆ‰ys  | injâ‚ zâˆˆxs-ys = injâ‚ ( (there zâˆˆxs-ys) )

  set-subtract-notin : âˆ€ {xs ys z} â†’ z âˆˆ xs â†’ z âˆ‰ ys â†’ z âˆˆ set-subtract xs ys
  set-subtract-notin{xs}{ys}{z} zâˆˆ zâˆ‰ with set-subtract-split{xs}{ys}{z} zâˆˆ
  ... | injâ‚ a = a
  ... | injâ‚‚ b = âŠ¥-elim (zâˆ‰ b)

-- note: the fixity of our triple-apply _#_ combinator
-- is different from the usual fixity of _$_.
infixl 1 _#_

-- applying three functions to three values in parallel
_#_ : {A B C : Set} â†’ {D : A â†’ Set} â†’ {E : B â†’ Set} â†’ {F : C â†’ Set} â†’
  ((a : A) â†’ D a) Ã— ((b : B) â†’ E b) Ã— ((c : C) â†’ F c) â†’
  (abc : A Ã— B Ã— C) â†’ D (projâ‚ abc) Ã— E (projâ‚ (projâ‚‚ abc)) Ã— F (projâ‚‚ (projâ‚‚ abc))
(f , g , h) # (x , y , z) = f x ,â€² g y ,â€² h z

infix 6 _UÌ¬_

_UÌ¬_ : List â„• Ã— List â„• Ã— List â„• â†’ List â„• Ã— List â„• Ã— List â„• â†’ List â„• Ã— List â„• Ã— List â„•
(S , s , v) UÌ¬ (S1 , s1 , v1) = S ++ S1 ,â€² s ++ s1 ,â€² v ++ v1
infixr 6 _|ÌŒ_
infixr 6 _|Â¹_
_|Â¹_ = ListSet.set-subtract Data.Nat._â‰Ÿ_
_|ÌŒ_ : List â„• Ã— List â„• Ã— List â„• â†’ List â„• Ã— List â„• Ã— List â„• â†’ List â„• Ã— List â„• Ã— List â„•
(S , s , x) |ÌŒ (S1 , s1 , x1)
   = (S |Â¹ S1) , (s |Â¹ s1) , (x |Â¹ x1)

âˆª-assoc : âˆ€ xsÂ³ ysÂ³ zsÂ³ â†’ (xsÂ³ UÌ¬ ysÂ³) UÌ¬ zsÂ³ â‰¡ xsÂ³ UÌ¬ (ysÂ³ UÌ¬ zsÂ³)
âˆª-assoc xsÂ³ ysÂ³ zsÂ³
  rewrite ++-assoc (projâ‚ xsÂ³)         (projâ‚ ysÂ³)         (projâ‚ zsÂ³)
        | ++-assoc (projâ‚ (projâ‚‚ xsÂ³)) (projâ‚ (projâ‚‚ ysÂ³)) (projâ‚ (projâ‚‚ zsÂ³))
        | ++-assoc (projâ‚‚ (projâ‚‚ xsÂ³)) (projâ‚‚ (projâ‚‚ ysÂ³)) (projâ‚‚ (projâ‚‚ zsÂ³)) = refl



xs++[]â‰¡xs : âˆ€{A : Set} â†’ (xs : List A) â†’ xs ++ [] â‰¡ xs
xs++[]â‰¡xs xs = projâ‚‚ (Monoid.identity (Data.List.Properties.++-monoid _)) xs

xâˆˆxs++[]â†’xâˆˆxs : âˆ€{A} â†’ {x : A} â†’ {xs : List A} â†’ x âˆˆ (xs ++ []) â†’ x âˆˆ xs
xâˆˆxs++[]â†’xâˆˆxs {xs = xs} xâˆˆxs rewrite xs++[]â‰¡xs xs = xâˆˆxs



_âŠ†Â¹_ : List â„• â†’ List â„• â†’ Set
xs âŠ†Â¹ ys = âˆ€ x â†’ x âˆˆ xs â†’ x âˆˆ ys

_âŠ†_ : List â„• Ã— List â„• Ã— List â„• â†’ List â„• Ã— List â„• Ã— List â„• â†’ Set
(Ssâ‚ , ssâ‚ , xsâ‚) âŠ† (Ssâ‚‚ , ssâ‚‚ , xsâ‚‚) =
  (Ssâ‚ âŠ†Â¹ Ssâ‚‚) Ã— (ssâ‚ âŠ†Â¹ ssâ‚‚) Ã— (xsâ‚ âŠ†Â¹ xsâ‚‚)



âŠ†Â¹-refl : âˆ€ {xs} â†’ xs âŠ†Â¹ xs
âŠ†Â¹-refl _ p = p

âŠ†-refl : âˆ€ {xsÂ³} â†’ xsÂ³ âŠ† xsÂ³
âŠ†-refl = âŠ†Â¹-refl ,â€² âŠ†Â¹-refl ,â€² âŠ†Â¹-refl

âŠ†-empty-left : âˆ€ {xsÂ³} â†’ ([] ,â€² [] ,â€² []) âŠ† xsÂ³
âŠ†-empty-left = (Î» _ ()) ,â€² (Î» _ ()) ,â€² (Î» _ ())

âŠ†Â¹-trans : âˆ€{xs ys zs} â†’ xs âŠ†Â¹ ys â†’ ys âŠ†Â¹ zs â†’ xs âŠ†Â¹ zs
âŠ†Â¹-trans xsâŠ†ys ysâŠ†zs w wâˆˆxs = ysâŠ†zs _ (xsâŠ†ys _ wâˆˆxs)

âŠ†-trans : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ âŠ† ysÂ³ â†’ ysÂ³ âŠ† zsÂ³ â†’ xsÂ³ âŠ† zsÂ³
âŠ†-trans xsÂ³âŠ†ysÂ³ ysÂ³âŠ†zsÂ³ = âŠ†Â¹-trans ,â€² âŠ†Â¹-trans ,â€² âŠ†Â¹-trans # xsÂ³âŠ†ysÂ³ # ysÂ³âŠ†zsÂ³

âŠ†Â¹-respect-|Â¹ : âˆ€{xs ys} zs â†’ xs âŠ†Â¹ ys â†’ (xs |Â¹ zs) âŠ†Â¹ (ys |Â¹ zs)
âŠ†Â¹-respect-|Â¹{xs}{ys} zs xsâŠ‚ys a aâˆˆ with ListSet.set-subtract-merge Data.Nat._â‰Ÿ_ {xs}{zs}{a} aâˆˆ
... | (aâˆˆxs , aâˆ‰zs) = ListSet.set-subtract-notin Data.Nat._â‰Ÿ_ (xsâŠ‚ys a aâˆˆxs) aâˆ‰zs

âŠ†-respect-|ÌŒ : âˆ€{xs ys} zs â†’ xs âŠ† ys â†’ (xs |ÌŒ zs) âŠ† (ys |ÌŒ zs)
âŠ†-respect-|ÌŒ zs sub = âŠ†Â¹-respect-|Â¹ , âŠ†Â¹-respect-|Â¹  , âŠ†Â¹-respect-|Â¹  # zs # sub

âˆ·-respect-âŠ†Â¹ : âˆ€{xs ys} z â†’ xs âŠ†Â¹ ys â†’ (z âˆ· xs) âŠ†Â¹ (z âˆ· ys)
âˆ·-respect-âŠ†Â¹ z sub .z (here refl) = here refl
âˆ·-respect-âŠ†Â¹ z sub a (there aâˆˆ) = there (sub a aâˆˆ)

âˆªË¡ : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ âŠ† ysÂ³ â†’ xsÂ³ âŠ† (ysÂ³ UÌ¬ zsÂ³)
âˆªË¡ (Ssâ‚âŠ†Ssâ‚‚ , ssâ‚âŠ†ssâ‚‚ , xsâ‚âŠ†xsâ‚‚) =
  (Î» S â†’ ++Ë¡ âˆ˜ Ssâ‚âŠ†Ssâ‚‚ S) ,â€²
  (Î» s â†’ ++Ë¡ âˆ˜ ssâ‚âŠ†ssâ‚‚ s) ,â€²
  (Î» x â†’ ++Ë¡ âˆ˜ xsâ‚âŠ†xsâ‚‚ x)

âˆªÊ³ : âˆ€{xsÂ³ ysÂ³} â†’ (zsÂ³ : List â„• Ã— List â„• Ã— List â„•) â†’ xsÂ³ âŠ† ysÂ³ â†’ xsÂ³ âŠ† (zsÂ³ UÌ¬ ysÂ³)
âˆªÊ³ xs (Ssâ‚âŠ†Ssâ‚‚ , ssâ‚âŠ†ssâ‚‚ , xsâ‚âŠ†xsâ‚‚) =
  (Î» S â†’ ++Ê³ (projâ‚ xs)         âˆ˜ Ssâ‚âŠ†Ssâ‚‚ S) ,â€²
  (Î» s â†’ ++Ê³ (projâ‚ (projâ‚‚ xs)) âˆ˜ ssâ‚âŠ†ssâ‚‚ s) ,â€²
  (Î» x â†’ ++Ê³ (projâ‚‚ (projâ‚‚ xs)) âˆ˜ xsâ‚âŠ†xsâ‚‚ x)

âˆªÂ¹-unjoin-âŠ†Â¹ : âˆ€ {ys zs} xs â†’ (xs ++ ys) âŠ†Â¹ zs â†’ xs âŠ†Â¹ zs Ã— ys âŠ†Â¹ zs
âˆªÂ¹-unjoin-âŠ†Â¹ xs xs++ysâŠ†zs = (Î» x xâˆˆxs â†’ xs++ysâŠ†zs x (++Ë¡ xâˆˆxs)) ,â€²
                           (Î» x xâˆˆys â†’ xs++ysâŠ†zs x (++Ê³ xs xâˆˆys))

âˆª-unjoin-âŠ† : âˆ€ {ysÂ³ zsÂ³} xsÂ³ â†’ (xsÂ³ UÌ¬ ysÂ³) âŠ† zsÂ³ â†’ xsÂ³ âŠ† zsÂ³ Ã— ysÂ³ âŠ† zsÂ³
âˆª-unjoin-âŠ† xsÂ³ xsÂ³âˆªysÂ³âŠ†zsÂ³ = unzipÂ³ (âˆªÂ¹-unjoin-âŠ†Â¹ ,â€² âˆªÂ¹-unjoin-âŠ†Â¹ ,â€² âˆªÂ¹-unjoin-âŠ†Â¹ #
                                      xsÂ³ # xsÂ³âˆªysÂ³âŠ†zsÂ³)
  where unzipÂ³ : {A B C D E F : Set} â†’ (A Ã— D) Ã— (B Ã— E) Ã— (C Ã— F) â†’ (A Ã— B Ã— C) Ã— (D Ã— E Ã— F)
        unzipÂ³ ((a , d) , (b , e) , (c , f)) = (a , b , c) , (d , e , f)

âˆª-unjoin-âŠ†Ë¡ : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ (xsÂ³ UÌ¬ ysÂ³) âŠ† zsÂ³ â†’ xsÂ³ âŠ† zsÂ³
âˆª-unjoin-âŠ†Ë¡ {xsÂ³} = projâ‚ âˆ˜ âˆª-unjoin-âŠ† xsÂ³

âˆª-unjoin-âŠ†Ê³ : âˆ€ {ysÂ³ zsÂ³} xsÂ³ â†’ (xsÂ³ UÌ¬ ysÂ³) âŠ† zsÂ³ â†’ ysÂ³ âŠ† zsÂ³
âˆª-unjoin-âŠ†Ê³ xsÂ³ = projâ‚‚ âˆ˜ âˆª-unjoin-âŠ† xsÂ³

âˆªÂ¹-join-âŠ†Â¹ : âˆ€{xs ys zs} â†’ xs âŠ†Â¹ zs â†’ ys âŠ†Â¹ zs â†’ (xs ++ ys) âŠ†Â¹ zs
âˆªÂ¹-join-âŠ†Â¹ {xs} xsâŠ†zs ysâŠ†zs w wâˆˆxs++ys with ++â» xs wâˆˆxs++ys
... | injâ‚ wâˆˆxs = xsâŠ†zs _ wâˆˆxs
... | injâ‚‚ wâˆˆys = ysâŠ†zs _ wâˆˆys

âˆª-join-âŠ† : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ âŠ† zsÂ³ â†’ ysÂ³ âŠ† zsÂ³ â†’ (xsÂ³ UÌ¬ ysÂ³) âŠ† zsÂ³
âˆª-join-âŠ† xsÂ³âŠ†zsÂ³ ysÂ³âŠ†zsÂ³ = âˆªÂ¹-join-âŠ†Â¹ ,â€² âˆªÂ¹-join-âŠ†Â¹ ,â€² âˆªÂ¹-join-âŠ†Â¹ # xsÂ³âŠ†zsÂ³ # ysÂ³âŠ†zsÂ³

âˆª-respect-âŠ†-left : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ âŠ† ysÂ³ â†’ (xsÂ³ UÌ¬ zsÂ³) âŠ† (ysÂ³ UÌ¬ zsÂ³)
âˆª-respect-âŠ†-left {xsÂ³} {ysÂ³} {zsÂ³} xsÂ³âŠ†ysÂ³ = âˆª-join-âŠ† (âˆªË¡ xsÂ³âŠ†ysÂ³) (âˆªÊ³ ysÂ³ âŠ†-refl)

âˆª-respect-âŠ†-right : âˆ€{xsÂ³ ysÂ³} zsÂ³ â†’ xsÂ³ âŠ† ysÂ³ â†’ (zsÂ³ UÌ¬ xsÂ³) âŠ† (zsÂ³ UÌ¬ ysÂ³)
âˆª-respect-âŠ†-right {xsÂ³} {ysÂ³} zsÂ³ xsÂ³âŠ†ysÂ³ = âˆª-join-âŠ† (âˆªË¡ (âŠ†-refl {zsÂ³})) (âˆªÊ³ zsÂ³ xsÂ³âŠ†ysÂ³)

âˆªÂ¹-respect-âŠ†Â¹-right : âˆ€ {xs ys} zs â†’ xs âŠ†Â¹ ys â†’ (zs ++ xs) âŠ†Â¹ (zs ++ ys)
âˆªÂ¹-respect-âŠ†Â¹-right zs xsâŠ†ys = projâ‚ (âˆª-respect-âŠ†-right (zs ,â€² [] ,â€² []) (xsâŠ†ys ,â€² âŠ†Â¹-refl {[]} ,â€² âŠ†Â¹-refl {[]}))

âˆªÂ¹-respect-âŠ†Â¹-left : âˆ€ {xs ys zs} â†’ xs âŠ†Â¹ ys â†’ (xs ++ zs) âŠ†Â¹ (ys ++ zs)
âˆªÂ¹-respect-âŠ†Â¹-left{zs = zs} xsâŠ†ys =  projâ‚ (âˆª-respect-âŠ†-left{zsÂ³ = (zs ,â€² [] ,â€² [])} (xsâŠ†ys ,â€² âŠ†Â¹-refl {[]} ,â€² âŠ†Â¹-refl {[]})) 

âŠ†-subst-left : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ â‰¡ ysÂ³ â†’ xsÂ³ âŠ† zsÂ³ â†’ ysÂ³ âŠ† zsÂ³
âŠ†-subst-left refl xsÂ³âŠ†zsÂ³ = xsÂ³âŠ†zsÂ³

âˆª-comm-âŠ†-left : âˆ€ {ysÂ³ zsÂ³} xsÂ³ â†’ (xsÂ³ UÌ¬ ysÂ³) âŠ† zsÂ³ â†’ (ysÂ³ UÌ¬ xsÂ³) âŠ† zsÂ³
âˆª-comm-âŠ†-left xsÂ³ xsÂ³âˆªysÂ³âŠ†zsÂ³ =
  âˆª-join-âŠ† (projâ‚‚ (âˆª-unjoin-âŠ† xsÂ³ xsÂ³âˆªysÂ³âŠ†zsÂ³)) (projâ‚ (âˆª-unjoin-âŠ† xsÂ³ xsÂ³âˆªysÂ³âŠ†zsÂ³))

âˆª-comm-âŠ†-right : âˆ€ {xsÂ³ ysÂ³} zsÂ³ â†’ xsÂ³ âŠ† (ysÂ³ UÌ¬ zsÂ³) â†’ xsÂ³ âŠ† (zsÂ³ UÌ¬ ysÂ³)
âˆª-comm-âŠ†-right zsÂ³ xsÂ³âŠ†ysÂ³âˆªzsÂ³ = âŠ†-trans xsÂ³âŠ†ysÂ³âˆªzsÂ³ (âˆª-join-âŠ† (âˆªÊ³ zsÂ³ âŠ†-refl) (âˆªË¡ âŠ†-refl))



distinct' : âˆ€{A : Set} -> (xs : List A) -> (ys : List A) -> Set
distinct'{A} xs ys = (z : A) -> z âˆˆ xs -> z âˆˆ ys -> âŠ¥

distinct : (xs : List â„• Ã— List â„• Ã— List â„•) -> (ys : List â„• Ã— List â„• Ã— List â„•) -> Set
distinct (S , s , v) (S1 , s1 , v1) = distinct' S S1 Ã— distinct' s s1 Ã— distinct' v v1

distinct-empty-left : âˆ€ {xsÂ³} â†’ distinct ([] ,â€² [] ,â€² []) xsÂ³
distinct-empty-left = (Î» _ ()) ,â€² (Î» _ ()) ,â€² (Î» _ ())

distinct-empty-right : âˆ€ {xsÂ³} â†’ distinct xsÂ³ ([] ,â€² [] ,â€² [])
distinct-empty-right = (Î» _ _ ()) ,â€² (Î» _ _ ()) ,â€² (Î» _ _ ())

distinct'-sym : âˆ€{A L1 L2} â†’ (distinct'{A} L1 L2) â†’ (distinct'{A} L2 L1)
distinct'-sym d = Î» z x xâ‚ â†’ d z xâ‚ x

distinct'-to-left : {xs ys zs : List â„•} â†’
  (distinct' xs ys â†’ distinct' xs zs) â†’
  distinct' ys xs â†’ distinct' zs xs
distinct'-to-left f ysâ‰ xs = distinct'-sym (f (distinct'-sym ysâ‰ xs))

distinct-sym : âˆ€{VL1 VL2} â†’ distinct VL1 VL2 â†’ distinct VL2 VL1
distinct-sym (a , b , c) = (distinct'-sym a) , ((distinct'-sym b) , (distinct'-sym c))

dist'++Ë¡ : âˆ€{A V1 V2 V3} â†’ (distinct'{A} V1 (V2 ++ V3)) â†’ (distinct'{A} V1 V2)
dist'++Ë¡ {A} {V1} {V2} {V3} d = Î» z x xâ‚ â†’ d z x (++Ë¡ xâ‚)

dist'++Ê³ : âˆ€{A V1 V2 V3} â†’ (distinct'{A} V1 (V2 ++ V3)) â†’ (distinct'{A} V1 V3)
dist'++Ê³ {A} {V1} {V2} {V3} d = Î» z x xâ‚ â†’ d z x (++Ê³ V2 xâ‚)


dist'++b : âˆ€{A V1 V2 V3 V4} â†’ (distinct'{A} (V1 ++ V2) (V3 ++ V4)) â†’ (distinct'{A} V1 V3)
dist'++b d = Î» z x xâ‚ â†’ d z (++Ë¡ x) (++Ë¡ xâ‚)

dist':: : âˆ€{A V1 V2 S} â†’ (distinct'{A} V1 (S âˆ· V2)) â†’ (distinct' V1 V2)
dist'::{A}{V1}{V2}{S} d = dist'++Ê³{A}{V1}{[ S ]}{V2} d

dist'-sym : âˆ€{A L1 L2} â†’ (distinct'{A} L1 L2) â†’ (distinct' L2 L1)
dist'-sym d = Î» z x xâ‚ â†’  d z xâ‚ x

distinct'-union-left : âˆ€ {xs ys zs : List â„•} â†’
  distinct' xs zs â†’ distinct' ys zs â†’ distinct' (xs ++ ys) zs
distinct'-union-left {xs} xsâ‰ zs ysâ‰ zs x xâˆˆxs++ys xâˆˆzs with ++â» xs xâˆˆxs++ys
... | injâ‚ xâˆˆxs = xsâ‰ zs x xâˆˆxs xâˆˆzs
... | injâ‚‚ xâˆˆys = ysâ‰ zs x xâˆˆys xâˆˆzs

distinct-union-left : âˆ€ {xs ys zs} â†’
  distinct xs zs â†’ distinct ys zs â†’ distinct (xs UÌ¬ ys) zs
distinct-union-left xsâ‰ zs xsâ‰ ys =
  distinct'-union-left ,â€² distinct'-union-left ,â€² distinct'-union-left #
    xsâ‰ zs # xsâ‰ ys

distinct-union-right : âˆ€ {xs ys zs} â†’
  distinct zs xs â†’ distinct zs ys â†’ distinct zs (xs UÌ¬ ys)
distinct-union-right zsâ‰ xs zsâ‰ ys =
  distinct-sym (distinct-union-left (distinct-sym zsâ‰ xs) (distinct-sym zsâ‰ ys))

âŠ†Â¹-respect-distinct'-left : âˆ€{xs ys zs} â†’ xs âŠ†Â¹ ys â†’ distinct' ys zs â†’ distinct' xs zs
âŠ†Â¹-respect-distinct'-left xsâŠ†ys ysâ‰ zs x xâˆˆxs xâˆˆzs = ysâ‰ zs x (xsâŠ†ys x xâˆˆxs) xâˆˆzs

âŠ†-respect-distinct-left : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ âŠ† ysÂ³ â†’ distinct ysÂ³ zsÂ³ â†’ distinct xsÂ³ zsÂ³
âŠ†-respect-distinct-left {xsÂ³} {ysÂ³} {zsÂ³} xsÂ³âŠ†ysÂ³ ysÂ³â‰ zsÂ³ =
  âŠ†Â¹-respect-distinct'-left ,â€² âŠ†Â¹-respect-distinct'-left ,â€² âŠ†Â¹-respect-distinct'-left #
    xsÂ³âŠ†ysÂ³ # ysÂ³â‰ zsÂ³

âŠ†Â¹-respect-distinct'-right : âˆ€{xs ys zs} â†’ xs âŠ†Â¹ ys â†’ distinct' zs ys â†’ distinct' zs xs
âŠ†Â¹-respect-distinct'-right xsâŠ†ys zsâ‰ ys =
  distinct'-sym (âŠ†Â¹-respect-distinct'-left xsâŠ†ys (distinct'-sym zsâ‰ ys))

âŠ†-respect-distinct-right : âˆ€{xsÂ³ ysÂ³ zsÂ³} â†’ xsÂ³ âŠ† ysÂ³ â†’ distinct zsÂ³ ysÂ³ â†’ distinct zsÂ³ xsÂ³
âŠ†-respect-distinct-right {xsÂ³} {ysÂ³} {zsÂ³} xsÂ³âŠ†ysÂ³ zsÂ³â‰ ysÂ³ =
  distinct-sym (âŠ†-respect-distinct-left xsÂ³âŠ†ysÂ³ (distinct-sym zsÂ³â‰ ysÂ³))


distinct'-dec : (xs ys : List â„•) -> Dec (âˆƒ Î» z -> z âˆˆ xs Ã— z âˆˆ ys)
distinct'-dec [] ys = no Î» { (z , () , zâˆˆys) }
distinct'-dec (x âˆ· xs) ys with ListAny.any (\ x' -> x Data.Nat.â‰Ÿ x') ys
distinct'-dec (x âˆ· xs) ys | yes p = yes (x , here refl , p)
distinct'-dec (x âˆ· xs) ys | no Â¬p with distinct'-dec xs ys
distinct'-dec (x âˆ· xs) ys | no Â¬p | yes (z , zâˆˆxs , xâˆˆys) = yes (z , there zâˆˆxs , xâˆˆys)
distinct'-dec (x âˆ· xs) ys | no Â¬pâ‚ | no Â¬p =
  no (\ { (.x , here refl , zâˆˆys) â†’ Â¬pâ‚ zâˆˆys ;
          (z , there zâˆˆxs , zâˆˆys) â†’ Â¬p (z , zâˆˆxs , zâˆˆys) } )

distinct-dec : (xs ys : List â„• Ã— List â„• Ã— List â„•) ->
  Dec ((âˆƒ Î» S â†’ (S âˆˆ projâ‚ xs) Ã— (S âˆˆ projâ‚ ys)) âŠ
       (âˆƒ Î» s â†’ (s âˆˆ projâ‚ (projâ‚‚ xs)) Ã— (s âˆˆ projâ‚ (projâ‚‚ ys))) âŠ
       (âˆƒ Î» x â†’ (x âˆˆ projâ‚‚ (projâ‚‚ xs)) Ã— (x âˆˆ projâ‚‚ (projâ‚‚ ys))))
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) with distinct'-dec S1 S2
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) | yes S1S2 = yes (injâ‚ S1S2)
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) | no Â¬S1S2 with distinct'-dec s1 s2
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) | no Â¬S1S2 | yes s1s2 =
  yes (injâ‚‚ (injâ‚ s1s2))
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) | no Â¬S1S2 | no Â¬s1s2 with distinct'-dec x1 x2
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) | no Â¬S1S2 | no Â¬s1s2 | yes x1x2 =
  yes (injâ‚‚ (injâ‚‚ x1x2))
distinct-dec (S1 , s1 , x1) (S2 , s2 , x2) | no Â¬S1S2 | no Â¬s1s2 | no Â¬x1x2 =
  no (Î» { (injâ‚ x) â†’ Â¬S1S2 x ; (injâ‚‚ (injâ‚ x)) â†’ Â¬s1s2 x ; (injâ‚‚ (injâ‚‚ y)) â†’ Â¬x1x2 y })

distinct-not-distinct-dec : âˆ€ {xs ys} -> distinct xs ys ->
      ((âˆƒ Î» S â†’ (S âˆˆ projâ‚ xs) Ã— (S âˆˆ projâ‚ ys)) âŠ
       (âˆƒ Î» s â†’ (s âˆˆ projâ‚ (projâ‚‚ xs)) Ã— (s âˆˆ projâ‚ (projâ‚‚ ys))) âŠ
       (âˆƒ Î» x â†’ (x âˆˆ projâ‚‚ (projâ‚‚ xs)) Ã— (x âˆˆ projâ‚‚ (projâ‚‚ ys))))
      -> âŠ¥
distinct-not-distinct-dec {xs} {ys} DXY (injâ‚ (NDXYS , NDXYs , NDXYx)) = (projâ‚ DXY) NDXYS  NDXYs  NDXYx
distinct-not-distinct-dec {xs} {ys} DXY (injâ‚‚ (injâ‚ (NDXYS , NDXYs , NDXYx))) = (projâ‚ (projâ‚‚ DXY)) NDXYS  NDXYs  NDXYx
distinct-not-distinct-dec {xs} {ys} DXY (injâ‚‚ (injâ‚‚ (NDXYS , NDXYs , NDXYx))) = (projâ‚‚ (projâ‚‚ DXY)) NDXYS  NDXYs  NDXYx

distinct-dec-is-distinct : âˆ€ {xs ys} ->
      (((âˆƒ Î» S â†’ (S âˆˆ projâ‚ xs) Ã— (S âˆˆ projâ‚ ys)) âŠ
        (âˆƒ Î» s â†’ (s âˆˆ projâ‚ (projâ‚‚ xs)) Ã— (s âˆˆ projâ‚ (projâ‚‚ ys))) âŠ
        (âˆƒ Î» x â†’ (x âˆˆ projâ‚‚ (projâ‚‚ xs)) Ã— (x âˆˆ projâ‚‚ (projâ‚‚ ys))))
        -> âŠ¥) -> distinct xs ys
distinct-dec-is-distinct {xs} {ys} FAIL =
  ((Î» z zâˆˆxs zâˆˆys â†’ FAIL (injâ‚ (z , zâˆˆxs , zâˆˆys))) ,
   (Î» z zâˆˆxs zâˆˆys â†’ FAIL (injâ‚‚ (injâ‚ (z , zâˆˆxs , zâˆˆys)))) ,
   (Î» z zâˆˆxs zâˆˆys â†’ FAIL (injâ‚‚ (injâ‚‚ (z , zâˆˆxs , zâˆˆys)))))

case_return_of_ : âˆ€ {a b} {A : Set a} (x : A) (B : A â†’ Set b) â†’ (âˆ€ x â†’ B x) â†’ B x
case x return B of f = f x

case_of_ : âˆ€ {a b} {A : Set a} {B : Set b} â†’ A â†’ (A â†’ B) â†’ B
case x of f = f x

typeof : âˆ€{ğ•}{A : Set ğ•} â†’ A â†’ Set ğ•
typeof{_}{A} _ = A

fst : âˆ€{l} {A B C : Set l} â†’ A Ã— B Ã— C â†’ A
fst = projâ‚

snd : âˆ€{l} {A B C : Set l} â†’ A Ã— B Ã— C â†’ B
snd = projâ‚ âˆ˜ projâ‚‚

thd : âˆ€{l} {A B C : Set l} â†’ A Ã— B Ã— C â†’ C
thd = projâ‚‚ âˆ˜ projâ‚‚

âˆˆ:: : âˆ€{A} {x y : A} â†’ x âˆˆ (y âˆ· []) â†’ x â‰¡ y
âˆˆ:: (here px) = px
âˆˆ:: (there ())

nâˆ‰map-suc-n-+ : âˆ€ n xs  â†’ Â¬ (n âˆˆ Data.List.map (suc n +_) xs)
nâˆ‰map-suc-n-+ n [] ()
nâˆ‰map-suc-n-+ n (x âˆ· xs) (here nâ‰¡sucâŸ¨n+xâŸ©) with cong (_âˆ¸ n) nâ‰¡sucâŸ¨n+xâŸ©
... | nâˆ¸nâ‰¡sucâŸ¨n+xâŸ©âˆ¸n rewrite nâˆ¸nâ‰¡0 n | +-comm n x | m+nâˆ¸nâ‰¡m (suc x) n with nâˆ¸nâ‰¡sucâŸ¨n+xâŸ©âˆ¸n
... | ()
nâˆ‰map-suc-n-+ n (x âˆ· xs) (there nâˆˆmap-suc-n-+) = nâˆ‰map-suc-n-+ n xs nâˆˆmap-suc-n-+

map-+-swap-suc : âˆ€ n xs â†’
  Data.List.map (_+_ n) (Data.List.map suc xs) â‰¡ Data.List.map suc (Data.List.map (_+_ n) xs)
map-+-swap-suc n xs
  rewrite sym (map-compose {g = _+_ n} {f = suc}      xs)
        |      map-cong (Î» m â†’ +-comm n (suc m))      xs
        |      map-cong (Î» m â†’ cong suc (+-comm m n)) xs
        |      map-compose {g = suc}   {f = _+_ n}    xs
  = refl

map-+-compose-suc : âˆ€ n xs â†’
  Data.List.map (_+_ n) (Data.List.map suc xs) â‰¡ Data.List.map (_+_ (suc n)) xs
map-+-compose-suc n xs
  rewrite map-+-swap-suc n xs
        | sym (map-compose {g = suc} {f = _+_ n} xs)
  = refl

{-

This module implements sets of proofs indexed
by some key `K`, such that each key is unique.
This is encoded in UniqueSet by a list of proofs
that each key does not appear in the remainder
of the set.

-}

module UniquedSet where
  open import Data.Product hiding (map ; curry)
  open import Data.Maybe using (Maybe ; just ; nothing)
  open import Relation.Binary.PropositionalEquality
    using ([_] ; inspect)

  {-
   Proves that for some key accessor `f`,
   that key only occurs once in the list
  -}
  data UniquedList {A : Set} {B : Set}  (f : (A â†’ B)) : List A â†’ Set where
    e : (UniquedList f [])
    c : (x : A) â†’ (xâ‚ : List A)
        â†’ (fxâˆ‰l : ((f x) âˆ‰ (Data.List.map f xâ‚)))
        â†’ (UniquedList f xâ‚)
        â†’ (UniquedList f (x âˆ· xâ‚))

  {- A List indexed by K, such that each `K` only occurs once -}
  record UniquedSet {K : Set} (a : K â†’ Set) : Set where
    constructor uniqued-set
    field
      lst : List (Î£[ n âˆˆ K ] (a n))
      unq : UniquedList projâ‚ lst

  {- Curry a function which takes an exploded UniqueSet
     to take an actual UniqueSet.

     Useful when the function must do induction over the UniquedSet,
     as constantly taking the set appart and putting it back together
     disagrees with the termination checker.
  -}
  curry : âˆ€{ğ•}{B : Set ğ•}{K}{A : K â†’ Set}
          â†’ (f : (lst : List (Î£[ n âˆˆ K ] (A n)))
          â†’ UniquedList projâ‚ lst â†’ B)
          â†’ (UniquedSet A) â†’ B
  curry f (uniqued-set a b) = f a b

  {- 
    Map some function `f2` over the elements of the UniqueList
    using `f3` as the new accessor function.
  -}
  map : {A : Set} {B : Set} {C : Set} {D : Set} {f1 : A â†’ B} {l : List A}
        â†’ (f2 : A â†’ C)
        â†’ (f3 : C â†’ D)
        â†’ (UniquedList f1 l)
        â†’ ((x : A) â†’ (l : List A)
        â†’ (f3 (f2 x)) âˆˆ (Data.List.map f3 (Data.List.map f2 l))
        â†’ (f1 x) âˆˆ (Data.List.map f1 l))
        â†’ (UniquedList f3 (Data.List.map f2 l))
  map f2 f3 e fix = e
  map{f1 = f1} f2 f3 (c x xâ‚ fxâˆ‰l l) fix = c (f2 x) (Data.List.map f2 xâ‚) (fxâˆ‰l âˆ˜ (fix x xâ‚)) (map f2 f3 l fix)

  {- A to encode may-maybe on UniqueLists -}
  lift-maybe-pair : âˆ€{B : Set} â†’ {A : B â†’ Set} â†’ (Î£ B (Maybe âˆ˜ A)) â†’ Maybe (Î£ B A)
  lift-maybe-pair (a , just x) = just (a , x)
  lift-maybe-pair (a , nothing) = nothing

  {- Removes `nothing`s from the list -}
  map-maybe : âˆ€{B} â†’ {A : B â†’ Set} â†’ {l : List (Î£ B (Maybe âˆ˜ A))}
              â†’ UniquedList projâ‚ l
              â†’ UniquedList projâ‚ (Data.List.mapMaybe lift-maybe-pair l)
  map-maybe {B} {A} {.[]} e = e
  map-maybe {B} {A} {.((b , nothing) âˆ· xâ‚)} (c (b , nothing) xâ‚ fxâˆ‰l ul) 
     =  map-maybe ul
  map-maybe {B} {A} {.((b , just x) âˆ· xâ‚)} (c (b , just x) xâ‚ fxâˆ‰l ul)
    =  c (b , x) (Data.List.mapMaybe lift-maybe-pair xâ‚) (ug xâ‚ fxâˆ‰l) (map-maybe ul) 
    where
    ug : âˆ€ (l : List $ Î£ B (Maybe âˆ˜ A)) â†’ b âˆ‰ (Data.List.map projâ‚ l) â†’ b âˆ‰ (Data.List.map projâ‚ (Data.List.mapMaybe lift-maybe-pair l))
    ug [] âˆ‰ ()
    ug ((b , nothing) âˆ· l) âˆ‰ âˆˆ = ug l (âˆ‰ âˆ˜ there) âˆˆ
    ug ((b , just x) âˆ· l) âˆ‰ (here px) = âˆ‰ (here px)
    ug ((b , just x) âˆ· l) âˆ‰ (there âˆˆ) = ug l (âˆ‰ âˆ˜ there) âˆˆ

  {- Remove `nothing`s from the set-}
  set-map-maybe : âˆ€{K}{A : K â†’ Set} â†’ UniquedSet (Maybe âˆ˜ A) â†’ UniquedSet A
  set-map-maybe u = uniqued-set (Data.List.mapMaybe lift-maybe-pair lst) (map-maybe unq)
    where open UniquedSet u

  {-
     map `fk` over the keys and `fa` over the values of the unique set.
     Requires the proof that fk is injective to maintain the proofs
     of uniqueness.
  -}
  set-map : âˆ€{K1 K2 : Set} {A1 : K1 â†’ Set} {A2 : K2 â†’ Set}
            â†’ UniquedSet{K1} A1
            â†’ (fk : K1 â†’ K2)
            â†’ (fa : âˆ€{k1} â†’ (A1 k1) â†’ (A2 (fk k1)))
            â†’ (âˆ€{a b} â†’ (fk a) â‰¡ (fk b) â†’ a â‰¡ b)
            â†’ UniquedSet{K2} A2
  set-map{K1}{K2}{A1}{A2} st fk fa fix
    =  uniqued-set (Data.List.map f2 lst)
         $ map f2 f3 unq ug
       where
         open UniquedSet st
         f2 = (Prod.map fk fa)
         f3 =  projâ‚
         ug : (x : âˆƒ A1 ) â†’ (l : List (âˆƒ A1))
              â†’ (f3 (f2 x)) âˆˆ (Data.List.map f3 (Data.List.map f2 l))
              â†’ (projâ‚ x) âˆˆ (Data.List.map projâ‚ l)
         ug x [] ()
         ug x (xâ‚ âˆ· l) (here px) = here $ fix px
         ug x (xâ‚ âˆ· l) (there xâˆˆ) = there (ug x l xâˆˆ)


